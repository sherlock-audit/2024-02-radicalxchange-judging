Agreeable Blood Flamingo

medium

# `EnglishPeriodAuctionInternal:: _cancelAllBids` function allows bidder to cancel the highest bid.

## Summary
`_cancelAllBids` function which allows a bidder to cancel all their bids, also allows to cancel the highest bid of the round.

## Vulnerability Detail
Using `_cancelAllBids` function the bidder can withdraw their highest bid without consequence. Because there is no check to see that the bidder is not the highest bidder of that particular round like the `_cancelBid` function have.

## Proof Of Concept
`_cancelBid` function which checks if bidder is the highest bidder.
```javascript
function _cancelBid(
        uint256 tokenId, //tokenID
        uint256 round, //round
        address bidder //bidder address
    ) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout(); 
        address currentBidder;
        if (IStewardLicense(address(this)).exists(tokenId)) {
            currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            currentBidder = l.initialBidder; 
        }

        require(
@>           bidder != l.highestBids[tokenId][round].bidder, //The bidder should not be the highest bidder of particular round.
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );
        //mapping from tokenid to roundNo to Bidder address to the bid
        Bid storage bid = l.bids[tokenId][round][bidder]; //getting bid of the bidder

        require(
            bid.collateralAmount > 0, //require the collateralAmount should be greater than 0
            'EnglishPeriodicAuction: No bid to cancel'
        );

        // Make collateral available to withdraw
        //mapping from bidder address to its available collateral to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount; //setting availableCollateral to withdraw by bidder

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```
`_cancelAllBids` function which does not check if bidder is the highest bidder.
```javascript
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout(); 

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId]; //getting current auctionRound

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
@>          //No check for bidder is the highest bidder. 
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                l.availableCollateral[bidder] += bid.collateralAmount; 

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

1. Imagine Alice is the initial bidder.
2. Alice know she can place the multiple bids and cancel those bids including the one which is the highest bid.
3. By taking benefit of this alice can increase the bidAmount without any consequence, because she know if no genuine bidder place the bid that overtake the highest bid of her, she can simply cancel her highest bid and get over with no consequence.

## Impact
The bidder can cancel there highest bid, which make the protocol work not as intended.

## Code Snippet
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416

## Tool used
Manual Review

## Recommendation
The recommended mitigation is to add an extra check in `_cancelAllBids` function which prevent the cancellation of highest bid

Recommended mitigation in code:
```diff
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout(); 

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId]; //getting current auctionRound

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
+          if(bidder ==  l.highestBids[tokenId][i].bidder){
+           continue;
+          }
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                l.availableCollateral[bidder] += bid.collateralAmount; 

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```