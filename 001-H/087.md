Fluffy Chili Copperhead

high

# The highest bidder can win the auction without paying the deposited collateral amount.

## Summary
A bidder can outbid the highest bidder and withdraw their bid right before the auction ends, maintaining their position as the highest bidder and ultimately winning the auction. This allows them to recover their collateral and potentially cause the protocol to lose funds belonging to other users.

## Vulnerability Detail
Follow this scenario : 
- The current highest bid amount is 2 ETH.
- A new malicious bidder surpasses this by bidding 2.5 ETH and becomes the new highest bidder.
- Normally, a bidder can cancel their bid by calling _cancelBid, but they can't cancel if they are the highest bidder.
- However, this check is forgotten in _cancelAllBids, so as the malicious bidder, right before the auction ends, you call cancelAllBids. This causes a desynchronization between the bids and highest bids structs, as the latter is not reset, allowing you to retain your position as the highest bidder while your collateral is freed.
- The malicious bidder then calls _withdrawCollateral and recovers their funds.
- They subsequently call _closeAuction to end the auction and obtain the Steward License, paying the fees using the contract funds, which currently hold other bidders' funds, leading to a loss of funds for other users. The malicious bidder does not lose their collateral and gains the Steward License.

```solidity
function _cancelBid(
        uint256 tokenId,
        uint256 round,
        address bidder
    ) internal {
    ...
        require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );
   ...
        // Make collateral available to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount;

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```
```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

## Impact
- loss of other biders funds
- obtaining Steward License without paying the collateral.
## Code Snippet
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L378-L411
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416-L434

## Tool used
Manual Review

## Recommendation
To prevent the scenario described, you can add a check in the cancelAllBids function to ensure that the highest bidder cannot cancel their bid.
