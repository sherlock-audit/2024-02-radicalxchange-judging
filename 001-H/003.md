Strong Lava Millipede

high

# An invocation of `_cancelAllBids()` will ignore if the `bidder` is the highest bidder for the current round.

## Summary

The highest bidder may call [`cancelAllBidsAndWithdrawCollateral(uint256)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187C14-L187C65) at any time, which is implemented by the [`_cancelAllBids(uint256,address)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416C14-L416C61) function; an operation which does not respect whether the caller is the highest bidder or not.

This enables the highest bid caller to steal the auction.

## Vulnerability Detail

When cancelling a singular bid via [`_cancelBid(uint256,uint256,address)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L378C5-L382C6), rigorous checks are enforced to ensure the validity of the cancellation state transition:

```solidity
/**
 * @notice Cancel bid for current round if not highest bidder
 */
function _cancelBid(
    uint256 tokenId,
    uint256 round,
    address bidder
) internal {
    EnglishPeriodicAuctionStorage.Layout
        storage l = EnglishPeriodicAuctionStorage.layout();

    address currentBidder;
    if (IStewardLicense(address(this)).exists(tokenId)) {
        currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
    } else {
        currentBidder = l.initialBidder;
    }

@>  require(
@>      bidder != l.highestBids[tokenId][round].bidder,
@>      'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
@>  );

    Bid storage bid = l.bids[tokenId][round][bidder];

    require(
        bid.collateralAmount > 0,
        'EnglishPeriodicAuction: No bid to cancel'
    );

    // Make collateral available to withdraw
    l.availableCollateral[bidder] += bid.collateralAmount;

    // Reset collateral and bid
    bid.collateralAmount = 0;
    bid.bidAmount = 0;
}
```

Importantly, under no circumstances should the the bid be cancelled if it corresponds to the highest bid of an auction.

Now let's compare with the batch equivalent, [`_cancelAllBids(uint256,address)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416C14-L416C61):

```solidity
/**
 * @notice Cancel bids for all rounds
 */
function _cancelAllBids(uint256 tokenId, address bidder) internal {
    EnglishPeriodicAuctionStorage.Layout
        storage l = EnglishPeriodicAuctionStorage.layout();

    uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

@>  for (uint256 i = 0; i <= currentAuctionRound; i++) { /// @audit up_to_and_including_current_round
        Bid storage bid = l.bids[tokenId][i][bidder];

        if (bid.collateralAmount > 0) {
            // Make collateral available to withdraw
            l.availableCollateral[bidder] += bid.collateralAmount;

            // Reset collateral and bid
            bid.collateralAmount = 0;
            bid.bidAmount = 0;
        }
    }
}
```

As we can see, there are no such checks are enforced for the batch method. The leading auction bidder may indeed exit their position via an unprotected call to [`cancelAllBidsAndWithdrawCollateral(uint256)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187C14-L187C65):

```solidity
/**
 * @notice Cancel all bids and withdraw collateral
 */
function cancelAllBidsAndWithdrawCollateral(uint256 tokenId) external {
  _cancelAllBids(tokenId, msg.sender);
  _withdrawCollateral(msg.sender);
}
```

It is therefore feasible for malicious user to submit a highly-competitive auction-winning bid with nothing at stake; they merely withdraw their stake prior to auction completion, but retain their position as `highestBidder` for the `currentRound`.

Since their position as `highestBidder` is still retained after withdrawing their capital, ownership of the [`IStewardLicense`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/license/IStewardLicense.sol) will transfer to the malicious user upon settlement of the auction:

```solidity
IStewardLicense(address(this)).triggerTransfer(
  oldBidder,
  l.highestBids[tokenId][currentAuctionRound].bidder,
  tokenId
);
```

## Impact

High.

## Code Snippet

```solidity
/**
 * @notice Cancel bids for all rounds
 */
function _cancelAllBids(uint256 tokenId, address bidder) internal {
    EnglishPeriodicAuctionStorage.Layout
        storage l = EnglishPeriodicAuctionStorage.layout();

    uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

    for (uint256 i = 0; i <= currentAuctionRound; i++) {
        Bid storage bid = l.bids[tokenId][i][bidder];

        if (bid.collateralAmount > 0) {
            // Make collateral available to withdraw
            l.availableCollateral[bidder] += bid.collateralAmount;

            // Reset collateral and bid
            bid.collateralAmount = 0;
            bid.bidAmount = 0;
        }
    }
}
```

## Tool used

Manual Review

## Recommendation

### ðŸ“„ [EnglishPeriodicAuctionInternal.sol](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol)

It is recommended that [`_cancelAllBids(uint256,address)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416C14-L416C61) is underlyingly implemented by the sanity checks defined in [`_cancelBid(uint256,uint256, address)`](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L378C5-L382C6).

```diff
/**
 * @notice Cancel bids for all rounds
 */
function _cancelAllBids(uint256 tokenId, address bidder) internal {
    EnglishPeriodicAuctionStorage.Layout
        storage l = EnglishPeriodicAuctionStorage.layout();

    uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

    for (uint256 i = 0; i <= currentAuctionRound; i++) {
        Bid storage bid = l.bids[tokenId][i][bidder];
+
+      /// @dev avoid revert on no-op bid cancellation
        if (bid.collateralAmount > 0) {
+
+           /// @dev cancel the bid (enforce sanity checks)
+           _cancelBid(tokenId, i, bidder); 
+
-            // Make collateral available to withdraw
-            l.availableCollateral[bidder] += bid.collateralAmount;
-
-            // Reset collateral and bid
-            bid.collateralAmount = 0;
-            bid.bidAmount = 0;
        }
    }
}
```
