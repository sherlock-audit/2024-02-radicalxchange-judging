Suave Ivory Liger

high

# missing checks in `_cancelAllBids()` let the highest bidder withdraw all his collateral

## Summary
Missing checks in `_cancelAllBids()` lets the highest bidder withdraw all his collateral.
This internal function is externally callable by any bidder through `cancelAllBidsAndWithdrawCollateral()`.

## Vulnerability Detail
Unlike `_cancelAllBids()`, the function `_cancelBid()` contains the neccessary check to make sure that the highest bidder wont withdraw their collateral.
More specifically, the following code is absent from `_cancelAllBids()`:
```js
        require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );
```

## Impact
Exploiting this lets a malicious bidder win the Stewardship for free or freeze the protocol if it does not contain enough ETH to distribute the fees.
## Code Snippet
The vulnerable function:
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416
```js
    function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```
`_cancelBid()`, which contains the necessary checks:
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L378
```js
    function _cancelBid(
        uint256 tokenId,
        uint256 round,
        address bidder
    ) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        address currentBidder;
        if (IStewardLicense(address(this)).exists(tokenId)) {
            currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            currentBidder = l.initialBidder;
        }

        require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );

        Bid storage bid = l.bids[tokenId][round][bidder];

        require(
            bid.collateralAmount > 0,
            'EnglishPeriodicAuction: No bid to cancel'
        );

        // Make collateral available to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount;

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```
They are externally called through:
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187
```js
    function cancelAllBidsAndWithdrawCollateral(uint256 tokenId) external {
        _cancelAllBids(tokenId, msg.sender);
        _withdrawCollateral(msg.sender);
    }
```
```js
    function cancelBid(uint256 tokenId, uint256 round) external {
        _cancelBid(tokenId, round, msg.sender);
    }
```

## Tool used

Manual Review

## Recommendation

Apply similar checks as in `_cancelBid()` in the for loop of `_cancelAllBids()`. Or include calls to `_cancelBid()` in the for loop.
