Plain Topaz Kangaroo

high

# Broken Invariant: highest bidder can cancel bid

## Summary
Broken Invariant: highest bidder can cancel bid

## Vulnerability Detail
Users can cancel bid by calling `EnglishPeriodicAuctionFacet.sol:cancelBid` or `EnglishPeriodicAuctionFacet.sol:cancelAllBidsAndWithdrawCollateral`. There is an important invariant that this caller canceling a bid must not be the current highest bidder when the auction period is still on to prevent unexpected behaviour. This check is implemented in the internal _cancelBid function:
```solidity
require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );
```
However, This can still be bypassed as the bidder can go ahead and call `cancelAllBidsAndWithdrawCollateral` which calls `_cancelAllBids` in which this invariant check is missing and will get executed causing the invariant to be broken:
```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

## Impact
highest bidder can cancel bid leading to broken Invaraint that can be bypassed

## Code Snippet
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L173
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L395
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L427

## Tool used

Manual Review

## Recommendation
Here is the fix:
```diff
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];
            
+           require(
+           bidder != l.highestBids[tokenId][i].bidder,
+           'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
+       );

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```