Micro Snowy Koala

high

# User can cancel bid even when he is the highest one to receive a token for free

## Summary

By using `_cancelBid`, a user can take back the provided collateral to the auction, but he mustn't be the highest bidder in order to do that.

## Vulnerability Detail

Instead using `_cancelBid`, a user can use `_cancelAllBids`, because there the check which ensures he is not the highest bidder is missing.

```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

Imagine the following scenario:

User has become the highest bidder and the auction has ended, now no new bets can be placed for this round. 

Since all is completed, someone calls `closeAuction`:

```solidity
function closeAuction(uint256 tokenId) external {
        require(
            _isReadyForTransfer(tokenId),
            'EnglishPeriodicAuction: auction is not over'
        );

        _closeAuction(tokenId);
    }
```

It ensured the round is complete and the tokenID is ready for transfer:

```solidity
function _closeAuction(uint256 tokenId) internal {

        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        address oldBidder;

        if (IStewardLicense(address(this)).exists(tokenId)) {
            oldBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            oldBidder = l.initialBidder;
        }

        // Set lastPeriodEndTime to the end of the current auction period
        uint256 licensePeriod = IPeriodicPCOParamsReadable(address(this))
            .licensePeriod();

        l.lastPeriodEndTime[tokenId] = block.timestamp;
        l.currentLicensePeriod[tokenId] = licensePeriod;

        if (l.highestBids[tokenId][currentAuctionRound].bidder == address(0)) {

            // No bids were placed, transfer to repossessor
            Bid storage repossessorBid = l.bids[tokenId][currentAuctionRound][
                l.repossessor
            ];

            repossessorBid.bidAmount = 0;
            repossessorBid.feeAmount = 0;
            repossessorBid.collateralAmount = 0;
            repossessorBid.bidder = l.repossessor;

            l.highestBids[tokenId][currentAuctionRound] = repossessorBid;

        } else if (

            l.highestBids[tokenId][currentAuctionRound].bidder != oldBidder

        ) {

            // Transfer bid to previous bidder's collateral
            l.availableCollateral[oldBidder] += l.highestBids[tokenId][currentAuctionRound].bidAmount;

            l.highestBids[tokenId][currentAuctionRound].collateralAmount = 0;

            l.bids[tokenId][currentAuctionRound][l.highestBids[tokenId][currentAuctionRound].bidder].collateralAmount = 0;

        } else {
            l.highestBids[tokenId][currentAuctionRound].collateralAmount = 0;
            l.bids[tokenId][currentAuctionRound][oldBidder].collateralAmount = 0;
        }

        emit AuctionClosed(
            tokenId,
            currentAuctionRound,
            l.highestBids[tokenId][currentAuctionRound].bidder,
            oldBidder,
            l.highestBids[tokenId][currentAuctionRound].bidAmount
        );

        // Reset auction
        l.currentAuctionLength[tokenId] = 0;
        l.currentAuctionRound[tokenId] = l.currentAuctionRound[tokenId] + 1;

        // Transfer to highest bidder
        IStewardLicense(address(this)).triggerTransfer(
            oldBidder,
            l.highestBids[tokenId][currentAuctionRound].bidder,
            tokenId
        );

        // Distribute fee to beneficiary
        if (l.highestBids[tokenId][currentAuctionRound].feeAmount > 0) {
            IBeneficiary(address(this)).distribute{
                value: l.highestBids[tokenId][currentAuctionRound].feeAmount
            }();
        }
    }
```

Here the put up collateral from the highest bider is transferred to the bid initiator in return for the tokenID.

However a the highest bidder can call `_cancelAllBids` right before someone calls `closeAuction` to take his collateral back. Which is not supposed to happen because that way he will receive the `tokenID` for free. But actually now the funds are taken from the contract itself not the highest bidder, because when cancelling the bid, when he is the highest one, the `highestBids` mapping isn't cleared, so the initial bidder will still receive his money but on behalf of the contract not the highest bidder. A malicious party can abuse this to drain the protocol.

## Impact

Malicious bidder can get a token for free.

## Code Snippet

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416-L434

## Tool used

Manual Review

## Recommendation

Add the same check for highest bidder like in `cancelBid`