Lucky Laurel Rook

high

# The highest bidder could cancel bid via `cancelAllBidsAndWithdrawCollateral` function

## Summary

The `_cancelAllBids` function fails to verify whether the bidder is the highest bidder in the round, allowing the highest bidder to cancel their bids despite being the highest.

## Vulnerability Detail

When canceling a bid, the `_cancelBid` function ensures that the highest bidder cannot cancel their bid.

```solidity
    function _cancelBid(
        uint256 tokenId,
        uint256 round,
        address bidder
    ) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        address currentBidder;
        if (IStewardLicense(address(this)).exists(tokenId)) {
            currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            currentBidder = l.initialBidder;
        }
//@audit highest bidder cannot cancel bid
        require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );

        Bid storage bid = l.bids[tokenId][round][bidder];

        require(
            bid.collateralAmount > 0,
            'EnglishPeriodicAuction: No bid to cancel'
        );

        // Make collateral available to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount;

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```

However, the `_cancelAllBids` function lacks checks for the *highest bidder*, allowing bidders to cancel their bid even if they are the highest bidder. Please refer to the following POC.

```solidity
    function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

### POC

Add the test to `pco-art/test/auction/EnglishPeriodicAuction.ts` and run it with `npx hardhat test`.

```diff
diff --git a/pco-art/test/auction/EnglishPeriodicAuction.ts b/pco-art/test/auction/EnglishPeriodicAuction.ts
index a4a4832..d93003b 100644
--- a/pco-art/test/auction/EnglishPeriodicAuction.ts
+++ b/pco-art/test/auction/EnglishPeriodicAuction.ts
@@ -1608,6 +1608,29 @@ describe('EnglishPeriodicAuction', function () {
       );
     });

+    it.only('highest bidder could still cancel bid via cancelAllBidsAndWithdrawCollateral', async function () {
+      // Auction start: Now - 200
+      // Auction end: Now + 100
+      const instance = await getInstance({
+        auctionLengthSeconds: 300,
+        initialPeriodStartTime: (await time.latest()) - 200,
+        licensePeriod: 1000,
+      });
+
+      const bidAmount = ethers.utils.parseEther('1.1');
+      const feeAmount = await instance.calculateFeeFromBid(bidAmount);
+      const collateralAmount = feeAmount.add(bidAmount);
+
+      await instance
+        .connect(bidder1)
+        .placeBid(0, bidAmount, { value: collateralAmount });
+
+      await instance
+      .connect(bidder1)
+      .cancelAllBidsAndWithdrawCollateral(0);
+
+    });
+
     it('should revert if highest bidder tries to cancel bid after auction ends', async function () {
       // Auction start: Now - 200
       // Auction end: Now + 100
```


## Impact

The highest bidder has the ability to cancel their bid, which could significantly impact the core functionality of the contract.

## Code Snippet

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L378-L411

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416-L434

## Tool used

Hardhat

## Recommendation

Add checks to the `_cancelAllBids` function to ensure that the highest bidder cannot cancel bid.
