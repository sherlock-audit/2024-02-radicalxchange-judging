Winning Opaque Dalmatian

high

# An attacker can steal the "collection" for free by the incorrect handling of the `EnglishPeriodicAuctionInternal.sol#_cancelAllBids()` method.

## Summary
The `EnglishPeriodicAuctionInternal.sol#_cancelAllBids()` method does not check whether the caller is the highest bidder. Therefore, the attacker can keep the "collection" for free.
## Vulnerability Detail
The `EnglishPeriodicAuctionInternal.sol#_cancelAllBids()` method and the `EnglishPeriodicAuctionInternal.sol#_cancelBid()` are functions that allows the caller to cancel his bids.

```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
423:        Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

```solidity
function _cancelBid(
        uint256 tokenId,
        uint256 round,
        address bidder
    ) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        address currentBidder;
        if (IStewardLicense(address(this)).exists(tokenId)) {
            currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            currentBidder = l.initialBidder;
        }

393:    require(
394:        bidder != l.highestBids[tokenId][round].bidder,
395:        'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
396:    );
397:
398:    Bid storage bid = l.bids[tokenId][round][bidder];

        require(
            bid.collateralAmount > 0,
            'EnglishPeriodicAuction: No bid to cancel'
        );

        // Make collateral available to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount;

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```
As you can see from the two methods on the right, the `_cancelAllBids()` method does not check whether the caller is the highest bidder.

Let's look at the example below.

1.Bob calls the placeBid() method. At this point, Bob becomes the highest bidder.
2. At this time, when the current auction ends, the closeAuction() method is called and the collection token is sent to Bob.
3.Next, Bob calls the cancelAllBidsAndWithdrawColtral() method. At this time, `_cancelAllBids()` method is called and Bob withdraws all his collateral.
As a result, Bob gets all of his collateral back and keeps the collection free.
## Impact
An attacker can keep the collection token for free.
## Code Snippet
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/459dfbfa73f74ed3422e894f6ff5fe2bbed146dd/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416-L434
## Tool used

Manual Review

## Recommendation
Add follow lines to the `EnglishPeriodicAuctionInternal.sol#_cancelAllBids()` method.
```solidity
function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

+++         if(bidder == l.highestBids[tokenId][i].bidder)
+++             continue;

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```
