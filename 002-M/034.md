Uneven Hickory Orangutan

medium

# Current owners have an unfair bidding leverage over other bidders

## Summary

A simplification in the bidding algorithm gives current owners a leverage in the process over other potential bidders.

## Vulnerability Detail

When placing a bid, users have to provide enough collateral to cover the current bid amount, and the associated fees. If the bid is the highest one when closing the auction, the bid amount goes to the previous owner, while the fees are distributed through the protocol.

There is a special case when the bidder is the current owner. In this scenario, if the bid is the highest when the auction closes, the bid amount would be transferred to the same account, since the bidder (or new owner) is the same as the previous owner. The implementation in this case takes a shortcut by requiring collateral just to cover the fee amount when the bidder is the current owner (`currentBidder`).

```solidity
uint256 totalCollateralAmount = bid.collateralAmount + collateralAmount;

uint256 feeAmount;
address currentBidder;
if (IStewardLicense(address(this)).exists(tokenId)) {
    currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
} else {
    currentBidder = l.initialBidder;
}

if (bidder == currentBidder) {
    // If current bidder, collateral is entire fee amount
    feeAmount = totalCollateralAmount;
} else {
    require(
        totalCollateralAmount > bidAmount,
        'EnglishPeriodicAuction: Collateral must be greater than current bid'
    );
    // If new bidder, collateral is bidAmount + fee
    feeAmount = totalCollateralAmount - bidAmount;
}

require(
    _checkBidAmount(bidAmount, feeAmount),
    'EnglishPeriodicAuction: Incorrect bid amount'
);
```

Note that when `bidder == currentBidder`, the whole collateral is taken as fees, and the `totalCollateralAmount > bidAmount` check is skipped. This may seem fine at first, since if the bid ends up being the highest bid, then the bid amount from the bidder account would go to the `availableCollateral` of the same account, so why would the implementation bother to ask for it in the first place. This is evidenced in `_closeAuction()`, when `highestBids[tokenId][currentAuctionRound].bidder == oldBidder`, the implementation simply resets the collateral amount, instead of transferring the bid amount to the `oldBidder` when the accounts are not the same.

```solidity
} else if (
    l.highestBids[tokenId][currentAuctionRound].bidder != oldBidder
) {
    // Transfer bid to previous bidder's collateral
    l.availableCollateral[oldBidder] += l
    .highestBids[tokenId][currentAuctionRound].bidAmount;
    l.highestBids[tokenId][currentAuctionRound].collateralAmount = 0;
    l
    .bids[tokenId][currentAuctionRound][
        l.highestBids[tokenId][currentAuctionRound].bidder
    ].collateralAmount = 0;
} else {
    l.highestBids[tokenId][currentAuctionRound].collateralAmount = 0;
    l
    .bids[tokenId][currentAuctionRound][oldBidder].collateralAmount = 0;
}
```

However, this creates a notable difference in the way the collateral can be leveraged by the current owner. The current owner can just deposit the fee, while others have to concretely provide the fee and the bid amount as well. For example, if the fee is say 1%, then the current owner can bid up to 100 ETH having just 1 ETH. For other bidders, this would imply having at hand 101 ETH.

## Impact

Current owners can leverage their collateral by just providing the fee costs, creating an unfair advantage over other bidders.

## Code Snippet

https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L325-L335

## Tool used

Manual Review

## Recommendation

Handle both cases the same and require all users to provide collateral for the bid and the fee amount. Eventually, if the highest bidder is the previous owner, then they can simply take back the placed bid amount.
