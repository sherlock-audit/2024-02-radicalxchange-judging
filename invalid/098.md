Fluffy Chili Copperhead

high

# The original SL owner can keep their position as the highest bidder by inflating their bid amount while only paying a fraction of it.

## Summary
The original SL owner can bid any amount and supply collateral enough for the fees only, allowing them to bid a higher amount while only paying the fee amount, which is a smaller fraction of the actual bid amount. This creates an unfair advantage for the highest bidder, as new users wishing to outbid them must pay the full bid amount plus fees.

## Vulnerability Detail
Following this scenario : 
for our scenario purpose we assume fee % is 10%

- The current Steward License (SL) owner wishes to retain ownership and participate in the auction.
- They bid 10 ETH while supplying only 10% as collateral, which is 1 ETH.
```solidity 
function placeBid(uint256 tokenId, uint256 bidAmount) external payable {
        ...
        _placeBid(tokenId, msg.sender, bidAmount, msg.value);
    }
```
- Since they are the SL owner, the placeBid function does not check whether the collateral amount covers the bid amount plus fees. Instead, it considers the full collateral amount as covering the fees, allowing the SL owner to bid without proving or paying that they can cover the full 10 ETH bid.

- This results in the SL owner inflating the bid price for the auction, making it more expensive for subsequent users to outbid them. Any user attempting to surpass the SL owner's bid must pay at least 10 ETH plus the minimal increment amount, potentially leading to losses for other users.

```solidity 
function _placeBid(
        uint256 tokenId,
        address bidder,
        uint256 bidAmount,
        uint256 collateralAmount
    ) internal {
       ...
        uint256 totalCollateralAmount = bid.collateralAmount + collateralAmount;

        if (bidder == currentBidder) {
            // If current bidder, collateral is entire fee amount
            feeAmount = totalCollateralAmount;
        } else {
            require(
                totalCollateralAmount > bidAmount,
                'EnglishPeriodicAuction: Collateral must be greater than current bid'
            );
            // If new bidder, collateral is bidAmount + fee
            feeAmount = totalCollateralAmount - bidAmount;
        }

        require(
            _checkBidAmount(bidAmount, feeAmount),
            'EnglishPeriodicAuction: Incorrect bid amount'
        );

        // Save bid
        bid.bidder = bidder;
        bid.bidAmount = bidAmount;
        bid.feeAmount = feeAmount;
        bid.collateralAmount = totalCollateralAmount;

        l.highestBids[tokenId][currentAuctionRound] = bid;
        ...
    }

/**
     * @notice Calculate fee from bid
     */
    function _calculateFeeFromBid(
        uint256 bidAmount
    ) internal view returns (uint256) {
        uint256 feeNumerator = IPeriodicPCOParamsReadable(address(this))
            .feeNumerator();
        uint256 feeDenominator = IPeriodicPCOParamsReadable(address(this))
            .feeDenominator();

        return (bidAmount * feeNumerator) / feeDenominator;
    }

    /**
     * @notice Check that fee is within rounding error of bid amount
     */
    function _checkBidAmount(
        uint256 bidAmount,
        uint256 feeAmount
    ) internal view returns (bool) {
        uint256 calculatedFeeAmount = _calculateFeeFromBid(bidAmount);

        return calculatedFeeAmount == feeAmount;
    }


```

## Impact
- inflating the auction bid price for other users
- retaining ownership of the SL while paying a fraction of the amount
## Code Snippet
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L286-L373
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L594-L603
https://github.com/sherlock-audit/2024-02-radicalxchange/blob/main/pco-art/contracts/auction/EnglishPeriodicAuctionInternal.sol#L608-L615

## Tool used
Manual Review

## Recommendation
Implementing a check if the current SL owner has enough collateral to cover his bid Amount + the fees even if he is going to pay himself to keep the auction fair for every other user wishing to participate in it.